```

一个程序设计语言是一个记号系统，如同自然语言一样，它的完整定义应包括语法和语义方面。一个语言的语法是指一组规则，用它可以形成和产生一个合适的程序。

自然语言存在不同程度的二义性，这种模糊、不确定的方式无法精确定义一门程序设计语言。必须设计一种准确无误地描述程序设计语言的语法结构，这种严谨、简洁、易读的形式规则描述的语言结构模型称为文法。

目前广泛使用的手段是上下文无关文法，最著名的文法描述形式是Backus-Naur范式（BNF），而其扩展形式EBNF则更加简洁、灵活。因此本语言的文法使用EBNF文法来进行描述。

//在文法中，<>内包含非终结符，[]中包含可选项，{}中包含重复0次至无数次的项，|表示可以选择其左右两边的任意一项，=表示“被定义为”，用单引号包含的内容为该符号本身。

本高级语言是C语言的子集，因此参考C语言的语法结构。本高级语言的文法定义如下：

程序可由任意多个全局定义组成。其文法定义为：
<program> = { <global_decl> }*

全局定义，包含enum定义、变量定义、数组定义，以及函数定义，因变量定义、数组定义和函数定义都有相同的首部，避免在识别时出现歧义，因此将它们合并为其他定义，再在其他定义中，将它们进行区分。其文法定义为：
<global_decl> = <enum_decl> | <other_decl> 
<other_decl> = <type> { '*' }* <id> <decl_tail>
<decl_tail> = <var_decl> | <arr_decl> | <func_decl>

本高级语言将enum的功能设定为方便地定义多个值可自增长的全局变量，因此其变量都处于全局作用域中。其文法定义为：
<enum_decl> = 'enum' '{' <id> [ '=' <number> ] { ',' <id> [ '=' <number> ] }* '}' ';'

变量定义中，只允许用常量进行初始化，并且允许在一条定义语句中定义多个变量。其文法定义为：
<var_decl> = [ '=' <number> ] { ',' { '*' }+ <id> [ '=' <number> ] }* ';'

数组定义允许用常量列表对数组进行初始化。其文法定义为：
<arr_decl> = '[' <number> ']' [ '{' [ <number> , { ',' <number> }* ] '}' ] ';'

函数定义包括函数参数定义和函数返回值定义。其文法定义为：
<func_decl> = '(' <func_param> ')' '{' <func_body> '}'
<func_param> = <null> | <type> { '*' }* <id> { ',' <type> { '*' }* <id> }*
<func_body> = { <local_var> | <statement> }*

局部变量定义类似于全局变量定义，但是允许使用表达式进行初始化，而不仅仅是常量。其文法定义为：
<local_var> = <type> { '*' }* <id> [ '=' <expr> ] { ',' { '*' }* <id> [ '=' <expr> ] }* ';'

语句包含if语句、while语句、return语句、块语句以表达式语句。其文法定义为：
<statement> = <if_stat> | <while_stat> | 'return' <expr> ';' |
    '{' <statement> '}' | <expr> ';'
<if_stat> = 'if' '(' <expr> ')' <statement>
<while_stat> = 'while' '(' <expr> ')' <statement>

在进行表达式文法定义之前，需要明确表达式中各种运算符的优先级。下面对高级语言中优先级进行了定义：
|        op        |        mean        |        opriority        |
|        =         |      assign brac   |        10               |
|        ||        |        logic or    |        9                |
|        &&        |        logic and   |        8                |
| > < >= <= == !=  |        logic comp  |        7                |
|        + -       |        add & sub   |        6                |
|        * / %     |   mul & div & mod  |        5                |
|  ! - & * ++ --   |         ...        |        4                |
|       ++ --      |    post inc & dec  |        3                |
|        ()        |        param       |        2                |
|      [] ()       | index & func call  |        1                |

定义好优先级后，就可以对表达式进行文法定义。其文法定义为：
<expr> = <assign_expr>

赋值语句优先级低于或语句，其包含两个逻辑“或”表达式操作数。赋值语句的左操作数只能是左值，但是在文法定义阶段无法描述左值，因此这个问题将在语义分析中处理。其文法定义为：
<assign_expr> = <or_expr> <assign_tail>
<assign_tail> = '=' <or_expr> <assign_tail> | <null>

逻辑“或”表达式包含两个逻辑“与”表达式操作数。其文法定义为：
<or_expr> = <and_expr> <or_tail>
<or_tail> = '||' <and_expr> <or_tail> | <null>

逻辑“与”表达式包含两个关系运算表达式操作数。其文法定义为：
<and_expr> = <cmp_expr> <and_tail>
<and_tail> = '&&' <cmp_expr> <and_tail> | <null>

关系表达式包含两个算术表达式操作数。其文法定义为：
<cmp_expr> = <alo_expr> <cmp_tail>
<cmp_tail> = <cmps> <alo_expr> <cmp_tail>
<cmps> = '<' | '>' | '<=' | '>=' | '==' | '!='

算术运算表达式包含两个乘除表达式操作数。其文法定义为：
<alo_expr> = <mul_expr> <alo_tail>
<alo_tail> = <adds> <mul_expr> | <null>
<adds> = '+' | '-'

乘除表达式包含两个因子表达式操作数。其文法定义为：
<mul_expr> = <factor_expr> <mul_tail>
<mul_tail> = <muls> <factor_expr> | <null>
<muls> = '*' | '/' | '%'

因子表达式可以是值表达式，也可以是包含一个前置运算符及一个因子表达式的表达式。其文法定义为：
<factor_expr> = <left_op> <factor_expr> | <val_expr>
<left_op> = '!' | '-' | '&' | '*' | '++' | '--'

值表达式包含一个元素表达式，以及可选的后置运算符。其文法定义为：
<val_expr> = <elem_expr> <rop>
<rop> = '++' | '--'

元素表达式则不包含任何运算符，是基本的操作数单元，如变量、数组、函数调用、括号表达式、常量等，因为这些内容都是以<id>开始，因此需要将<id>提取，并在<elem_tail>中进行区分。其文法定义为：
<elem_expr> = <id> <elem_tail>
<elem_tail> = '[' <expr> ']' | '(' <args> ')' | '(' <expr> ')' | <literal> | <null>
<args> = <expr> { ',' <expr> ] }* | <null>
<literal> = <number> | <char> | <string>

常量包括数字、字符和字符串。数字包括0-9共10个数字字符组成的串，以及描述其正负属性的符号。通常数字字符的长度是有限的，但是文法中无法描述数字的长度，因此在词法分析中处理这个问题。文法定义为：
<number> = <sign> { <num> }+
<sign> = [ '+' ] | [ '-' ]
<num> = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

字符包括各种可能的字符，字符串则是由双引号括起来的字符的集合。其文法定义如下：
<string> = '"' { <char> }* '"'

空串则不包含任何内容
<null> = 
