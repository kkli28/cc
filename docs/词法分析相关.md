> 词法记号

```
ERR:    错误
END:    文件结束标记
ID:     标识符
KW_INT:     int类型
KW_CHAR:    char类型
KW_VOID:    void类型
KW_EXTERN:  extern关键字
NUM:        常量数字
CH:         常量字符
STR:        常量字符串
NOT:        ! 运算符
LEA:        & 运算符
ADD:        + 运算符
SUB:        - 运算符
MUL:        * 运算符
DIV:        / 运算符
MOD:        % 运算符
INC:        ++ 运算符
DEC:        -- 运算符
GT:         > 运算符
GE:         >= 运算符
LT:         < 运算符
LE:         <= 运算符
EQU:        == 运算符
NEQU:       != 运算符
AND:        && 运算符
OR:         || 运算符
LPAREN:     ( 左括号
RPAREN:     ) 右括号
LBRACK:     [ 左方括号
RBRACK:     ] 右方括号
LBRACE:     { 左大括号
RBRACE:     } 右大括号
COMMA:      , 逗号
COLON:      : 冒号
SEMICON:    ; 分号
ASSIGN:     = 赋值
KW_IF:      if 关键字
KW_ELSE:    else 关键字
KW_SWITCH:  switch 关键字
KW_CASE:    case 关键字
KW_DEFAULT: default 关键字
KW_WHILE:   while 关键字
KW_DO:      do 关键字
KW_FOR:     for 关键字
KW_BREAK:   break 关键字
KW_CONTINUE: continue 关键字
KW_RETURN:  return 关键字
```

> 文法

```
程序
<program> --> <segment> <program> | null

语句
<segment> --> KW_EXTERN <type> <def>
            | <type> <def>

类型
<type> --> KW_INT | KW_CHAR | KW_VOID

变量定义
<defdata> --> ID <varrdef> | MUL ID <init>

数组 及 普通变量
<varrdef> --> LBRACK NUM RBRACK | <init>

初始化
<int> --> ASSIGN <expr> | null

变量定义中，除了第一个变量的剩余部分
<deflist> --> COMMA <defdata> <deflist> | SEMICON

定义
<def> --> ID <idtail> | MUL ID <init> <deflist>

标识符尾部
<idtail> --> <varrdef> <deflist> | LPAREN <para> RPAREN <funtail>

函数定义尾部
<funtail> --> SEMICON | <block>

参数定义（除去类型）
<paradata> --> MUL ID | ID <paradatatail>
<paradatatail> --> LBRACK NUM RBRACK | null

参数列表（除去第一个参数定义）
<paralist> --> COMMA <type> <paradata> <paralist> | null

参数
<para> --> <type> <paradata> <paralist> | null

语句块
<block> --> LBRACE <subprogram> RBRACE

子程序
<subprogram> --> <localdef> <subprogram> | <statement> <subprogram> | null

局部定义
<localdef> --> <type> <defdata> <deflist>

赋值表达式
<assexpr> --> <orexpr> <asstail>

赋值表达式尾部
<asstail> --> ASSIGN <orexpr> <asstail> | null

逻辑或表达式
<orexpr> --> <andexpr> <ortail>

逻辑或表达式尾部
<ortail> --> OR <andexpr> <ortail> | null

逻辑与表达式
<andtail> --> <cmpexpr> <andtail>

逻辑与表达式尾部
<andtail> --> AND <cmpexpr> <andtail> | null

关系表达式
<cmpexpr> --> <aloexpr> <cmptail>

关系表达式尾部
<cmptail> --> <cmps> <aloexpr> <cmptail> | null

关系运算符
<cmps> --> GT | GE | LT | LE | EQU | NEQU

算术表达式
<aloexpr> --> <item> <alotail>

算术表达式尾部
<alotail> --> <adds> <item> <alotail> | null

算术表达式加减运算符
<adds> --> ADD | SUB

算术表达式项
<item> --> <factor> <itemtail>

算术表达式项尾部
<itemtail> --> <muls> <factor> <itemtail> | null

算术表达式乘除模运算符
<muls> --> MUL | DIV | MOD

因子表达式
<factor> --> <lop> <factor> | <val>

前置运算符
<lop> --> NOT | SUB | LEA | MUL | INCR | DECR

值表达式
<val> --> <elem> <rop>

后置运算符
<rop> --> INCR | DECR

元素表达式
<elem> --> ID <idexpr> | LPAREN <expr> RPAREN | <literal>

标识符表达式
<idexpr> --> LBRACK <expr> RBRACK | LPAREN <realarg> RPAREN | null

参数列表
<realarg> --> <arg> <arglist> | null

参数列表（除去第一个参数）
<arglist> --> COMMA <arg> <arglist> | null

参数
<arg> --> <expr>

常量
<literal> --> NUM | CH | STR

表达式
<expr> --> <assexpr>

可为空的表达式
<altexpr> --> <expr> | null

语句
<statement> --> <altexpr> SEMICON | <whilestat> | <forstat> | <dowhilestat>
              | <ifstat> | <switchstat> | KW_BREAK SEMICON | KW_CONTINUE SEMICON
              | KW_RETURN <altexpr> SEMICON

while语句
<whilestat> --> KW_WHILE LPAREN <altexpr> RPAREN <block>

do-while语句
<dowhilestat> --> KW_DO <block> KW_WHILE LPAREN <altexpr> RPAREN SEMICON

for语句
<forstat> --> KW_FOR LPAREN <forinit> <altexpr> SEMICON <altexpr> RPAREN
              <block>

for语句的初始语句
for<init> --> <localdef> | <altexpr> SEMICON

if-else语句
<ifstat> --> KW_IF LPAREN <expr> RPAREN <block> <elsestat>

//=================== ??? else 部分应该有 else if(...)类型 =====================

if-else语句的else部分
<elsestat> --> KW_ELSE <block> | null 

switch语句
<switchstat> --> KW_SWITCH LPAREN <expr> RPAREN LBRACE <casestat> RBRACE

case语句块
<casestat> --> KW_CASE <caselabel> COLON <subprogram> <casestat>
             | KW_DEFAULT COLON <subprogram>

case标签
<caselabel> --> <literal>
```
